apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.transferwise.assignment.moviediscover"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        String sharedTestCodeDir = 'src/sharedTest/kotlin'
        test {
            java.srcDirs += sharedTestCodeDir
        }
        androidTest {
            java.srcDirs += sharedTestCodeDir
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin Stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Google Play Services
    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'

    // Jetpack Support Library
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.browser:browser:1.0.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // MvRx
    implementation 'com.airbnb.android:mvrx:1.0.2'

    // Epoxy
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    implementation "com.airbnb.android:epoxy-paging:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'


    // Fresco
    implementation 'com.facebook.fresco:fresco:2.0.0'

    // Shimmer/skeleton loading
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    //Service Locator
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"

    //Lottie animation support
    implementation "com.airbnb.android:lottie:3.0.1"

    //LocalDateTime backport
    implementation 'org.threeten:threetenbp:1.4.0'

    //Rating visualization
    implementation 'com.github.rjsvieira:circularView:1.3.0'

    //Http logging (higher version not supported older androids)
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:3.12.3'


    // Libraries to support tests

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation 'org.awaitility:awaitility-kotlin:4.0.0-rc1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.1.0'

    //newer version not compatible with existing version of okhttp
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.12.3'
    androidTestImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    //newer version has issue with existing framework
    androidTestImplementation 'org.awaitility:awaitility-kotlin:3.1.6'
}
